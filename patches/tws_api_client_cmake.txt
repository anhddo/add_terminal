file(GLOB SOURCES "*.cpp")
file(GLOB HEADERS "*.h")
set(twsapi_VERSION 9.79.02)

# Add proto sources
file(GLOB PROTO_SOURCES "${CMAKE_CURRENT_SOURCE_DIR}/../../proto/*.pb.cc")
file(GLOB PROTO_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/../../proto/*.pb.h")

add_library(twsapi SHARED ${SOURCES} ${PROTO_SOURCES})

# Add proto include directory
target_include_directories(twsapi PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/../../proto>
    $<INSTALL_INTERFACE:include/twsapi/proto>
)

# Link against protobuf
find_package(protobuf CONFIG REQUIRED)
target_link_libraries(twsapi PUBLIC protobuf::libprotobuf)
target_link_libraries(twsapi PUBLIC libbid)
# On Windows (including MinGW), link against Winsock 2
if( WIN32 )
    target_link_libraries( twsapi
        PUBLIC
        ws2_32
    )
endif()
if( MSVC )
    target_compile_options( twsapi PRIVATE /W3 )
else()
    target_compile_options( twsapi PRIVATE -Wall -Wextra -pedantic -Wno-unused-parameter )
endif()
set_target_properties(twsapi
    PROPERTIES
    PREFIX "lib"
    DEBUG_POSTFIX "d"
    LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib"
    PUBLIC_HEADER "${HEADERS}"
    WINDOWS_EXPORT_ALL_SYMBOLS ON
)
install(TARGETS twsapi EXPORT twsapi-targets PUBLIC_HEADER DESTINATION include/twsapi LIBRARY DESTINATION lib)
